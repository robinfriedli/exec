plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.4.10"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

group = "net.robinfriedli"
version = "1.0"
sourceCompatibility = "8"
targetCompatibility = "8"

compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    testCompile "org.testng:testng:7.3.0"
}

test {
    useTestNG()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId "exec"

            from(components.java)

            artifact tasks.sourceJar
        }
    }
}

def bintrayProperties = new Properties()
def bintrayPropertiesFile = file("src/main/resources/bintray.properties")
if (bintrayPropertiesFile.exists()) {
    bintrayPropertiesFile.withInputStream { bintrayProperties.load(it) }
} else {
    bintrayProperties.setProperty("bintray.user", "empty")
    bintrayProperties.setProperty("bintray.key", "empty")
}

bintray {
    user = bintrayProperties.get("bintray.user")
    key = bintrayProperties.get("bintray.key")
    publications = ["maven"]
    pkg {
        repo = rootProject.group
        name = "exec"
    }
}